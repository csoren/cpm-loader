        INCLUDE "c128.inc"
        INCLUDE "z80bios.inc"
        INCLUDE "cpmsys.inc"

Parm_KeyTable   EQU     $FD09
Parm_DataTables EQU     $FD0B

InitCodeLocation        EQU     $E000

        SECTION "Code",CODE

        XDEF    Z80Entry

ClearMemory: MACRO
        ld      hl,\1
        ld      de,\1+1
        ld      bc,\2-\1-1
        xor     a
        ld      (hl),a
        ldir
        ENDM

CopyMemory: MACRO
        ld      hl,\1
        ld      de,\3
        ld      bc,\2
        call    memcpy
        ENDM

Z80Entry:
        ld      sp,StackTop
        ld      a,$3F
        ld      (MMU_CR),a

        ClearMemory Segments_End,$FF00

        ld      hl,InitCodeStart
        ld      de,InitCodeLocation
        ld      bc,InitCodeSize
        ldir

        jp      InitCodeLocation

InitCodeStart:
        ORG     InitCodeLocation

        ClearMemory $1000,Segments_Start

        CopyMemory BIOS_8502,BIOS_8502_Size,CPM_8502
        CopyMemory BIOS_CPUSwitch,BIOS_CPUSwitch_Size,EnableZ80

        ld      a,$C9           ; Z80 RET
        ld      (Return6502),a

        call    Enable6502

        ld      hl,BIOS_MMUConfig_Last
        ld      bc,$D50A
        ld      d,BIOS_MMUConfig_Size
.initializeMMU
        ld      a,(hl)
        out     (c),a
        dec     hl
        dec     c
        dec     d
        jr      nz,.initializeMMU

        ld      a,$1A           ; VDC colors
        call    BIOS_Wait
        ld      a,$90
        out     (c),a

        ld      a,$83
        ld      (Parm_CurrentAtr80),a

        ld      a,$0E
        ld      (Parm_Attr40),a

        ld      hl,KEYTABLE_START
        ld      (Parm_KeyTable),hl
        ld      hl,DATATABLES_START
        ld      (Parm_DataTables),hl

        call    BIOS_InitCharset

        ld      a,$19
        ld      (Parm_PaintSize),a

        BCALL   PrtMsgBoth
        DB      $FF             ; Clear screen
        DB      $81             ; Line 1
        DB      $0A             ; Column 10
        DB      'BOOTING CP/M PLUS',0

        ld      bc,$D018
        ld      a,$B6
        out     (c),a

        BCALL   PrtMsgBoth
        DB      $8A,0,0

        ld      hl,InfoText
        call    BIOS_PrintText
        
        BCALL   PrtMsgBoth
        DB      $83,$0C
        DB      'COMMON CODE',0

        CopyMemory CommonCode,CommonCode_Size,COMMONCODE_START

        BCALL   PrtMsgBoth
        DB      $84,$0C
        DB      'BANKED CODE',0

        CopyMemory BankedCode,BankedCode_Size,BANKEDCODE_START

        BCALL   PrtMsgBoth
        DB      $85,$0C
        DB      'BIOS8502 CODE',0

        CopyMemory BIOS8502Code,BIOS8502Code_Size,BIOS8502CODE_START

        ld      hl,BIOS8502CODE_START
        ld      (ReturnZ80+1),hl

        BCALL   PrtMsgBoth
        DB      $86,$0C
        DB      'DATA TABLES',0

        CopyMemory DataTables,DataTables_Size,DATATABLES_START

        ld      hl,CPM_START_ADDRESS
        push    hl

        ld      hl,InitCodeLocation
        ld      de,InitCodeLocation+1
        ld      bc,COMMONCODE_START-InitCodeLocation-1
        xor     a
        ld      (hl),a
        jp      BIOS_LDIR_RET

Flash:  ld      bc,$D020
        xor     a
.flash        
        out     (c),a
        inc     a
        jr      .flash

memcpy:
        call    BIOS_CmpHlDe
        jr      c,.up
.down   ldir
        ret
.up     dec     bc
        ex      hl,de
        add     hl,bc
        ex      hl,de
        add     hl,bc
        inc     bc
        lddr
        ret        

InfoText:
        INCBIN  "info.bin"

InitCodeEnd:

        IF      InitCodeEnd>COMMONCODE_START
        ERROR   "Init code segment too large"
        ENDC

InitCodeSize    EQU     InitCodeEnd-InitCodeLocation

        SECTION "InitCode",BSS[InitCodeLocation]
        DS      InitCodeSize

; --
; -- CPM+.SYS Segments
; --
        SECTION "Segments",DATA[$4000]
Segments_Start:

DataTables:
        INCBIN  "tables.bin"
DataTables_End:
DataTables_Size EQU     DataTables_End-DataTables

BIOS8502Code:
        INCBIN  "bios8502code.bin"
BIOS8502Code_End:
BIOS8502Code_Size EQU   BIOS8502Code_End-BIOS8502Code

BankedCode:
        INCBIN  "bankedcode.bin"
BankedCode_End:
BankedCode_Size EQU     BankedCode_End-BankedCode

CommonCode:
        INCBIN  "commoncode.bin"
CommonCode_End:
CommonCode_Size EQU     CommonCode_End-CommonCode

Segments_End:


        SECTION "CPMVars",BSS[$2400]
Vars1_Start:
Parm_Temp:              DS      2
Parm_CursorOffset:      DS      2
Parm_OldOffset:         DS      1
Parm_PrtFlag:           DS      1
Parm_FlashPos:          DS      2
Parm_PaintSize:         DS      1
Parm_CharAdr40:         DS      2
Parm_CharCol40:         DS      1
Parm_CharRow40:         DS      1
Parm_Attr40:            DS      1
Parm_BgCol40:           DS      1
Parm_BdCol40:           DS      1
Parm_Rev40:             DS      1
Parm_CharAdr80:         DS      2
Parm_CharCol80:         DS      1
Parm_CharRow80:         DS      1
Parm_CurrentAtr80:      DS      1
Parm_BgCol80:           DS      1
Parm_CharColor80:       DS      1

        SECTION "CPM8502",BSS[$3000]

CPM_8502:       DS      BIOS_8502_Size

        SECTION "CPMVars2",BSS[$3C00]

Vars2_Start:
                DS      4
pDataTable:     DS      2
                DS      1
                DS      $70
StackTop:
