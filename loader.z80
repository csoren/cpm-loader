        INCLUDE "c128.inc"
        INCLUDE "cpmsys.inc"

BIOS_GetStartAndSize    EQU     $0331
BIOS_PrintAutoText      EQU     $0526
BIOS_PrintInfoText      EQU     $0534
BIOS_VDCCopyCharset     EQU     $05BD
BIOS_VDCSelectReg       EQU     $0945
BIOS_8502               EQU     $0D22
BIOS_8502_Size          EQU     $1C3
BIOS_CPUSwitch          EQU     $0EE5
BIOS_CPUSwitch_Size     EQU     31
BIOS_MMUConfig          EQU     $0FAA
BIOS_MMUConfig_Size     EQU     11
BIOS_MMUConfig_Last     EQU     BIOS_MMUConfig+BIOS_MMUConfig_Size-1

InitCodeLocation        EQU     $E000

        SECTION "Code",CODE

        XDEF    Z80Entry

ClearMemory: MACRO
        ld      hl,\1
        ld      de,\1+1
        ld      bc,\2-\1-1
        xor     a
        ld      (hl),a
        ldir
        ENDM

Z80Entry:
        ld      sp,$3C77
        ld      a,$3F
        ld      (MMU_CR),a

        ; Clear memory 3000-FEFF
        ;ClearMemory $3000,CpmSys
        ;ClearMemory CpmSysEnd,$FF00

        ; Copy 8502 code to $3000
        ld      hl,BIOS_8502
        ld      de,CPM_8502
        ld      bc,BIOS_8502_Size
        ldir

        ld      hl,BIOS_CPUSwitch
        ld      de,EnableZ80
        ld      bc,BIOS_CPUSwitch_Size
        ldir

        ld      hl,InitCodeStart
        ld      de,InitCodeLocation
        ld      bc,InitCodeSize
        ldir

        jp      InitCodeLocation

InitCodeStart:
        ORG     InitCodeLocation

        ld      a,$C9           ; Z80 RET
        ld      (Return6502),a
        call    Enable6502

        ld      hl,BIOS_MMUConfig_Last
        ld      bc,$D50A
        ld      d,BIOS_MMUConfig_Size
.initializeMMU
        ld      a,(hl)
        out     (c),a
        dec     hl
        dec     c
        dec     d
        jr      nz,.initializeMMU

;        ClearMemory $1000,$3000

        ld      a,$1A           ; VDC colors
        call    BIOS_VDCSelectReg
        ld      a,$90
        out     (c),a

        ld      a,$83
        ld      (VDCAttr),a

        ld      a,$0E
        ld      (VDCCharCol),a

        call    BIOS_VDCCopyCharset

        ld      a,$19
        ld      (Rows),a

        call    BIOS_PrintAutoText
        
        DB      $FF             ; Clear screen
        DB      $81             ; Line 1
        DB      $0A             ; Column 10
        DB      'BOOTING CP/M PLUS',0

        ld      bc,$D018
        ld      a,$B6
        out     (c),a

        call    BIOS_PrintAutoText
        DB      $8A,0,0

        ld      hl,InfoText
        call    BIOS_PrintInfoText
        
        call    BIOS_PrintAutoText
        DB      $83,$0C
        DB      'DATA TABLES',0

        call    BIOS_PrintAutoText
        DB      $84,$0C
        DB      'COMMON CODE',0

        call    BIOS_PrintAutoText
        DB      $85,$0C
        DB      'BANKED CODE',0

        call    BIOS_PrintAutoText
        DB      $86,$0C
        DB      'BIOS8502 CODE',0

        ld      hl,BIOS8502CODE_START
        ld      ($FFDD),hl

        ld      hl,CPM_START_ADDRESS
        jp      (hl)

        jp      Flash

Flash:  ld      bc,$D020
        xor     a
.flash        
        out     (c),a
        inc     a
        jr      .flash

InitCodeSize    EQU     @-InitCodeLocation

        SECTION "InitCode",BSS[InitCodeLocation]
        DS      InitCodeSize

        SECTION "CPMVars",BSS[$2400]

                DS      8
Rows            DS      1
                DS      4
VDCCharCol      DS      1
                DS      7
VDCAttr         DS      1


        SECTION "CPM8502",BSS[$3000]

CPM_8502:       DS      BIOS_8502_Size


        SECTION "InfoSegment",DATA
InfoText:       INCBIN  "info.bin"

        SECTION "BankedCode",DATA[BANKEDCODE_START]
        INCBIN  "bankedcode.bin"

        SECTION "8502",DATA[BIOS8502CODE_START]
        INCBIN  "bios8502code.bin"

        SECTION "CommonCode",DATA
CommonCodeStart:
        INCBIN  "commoncode.bin"
CommonCodeEnd:
        
        SECTION "DataTables",DATA
DataTablesStart:
        INCBIN  "tables.bin"
DataTablesEnd:
        